# samples tested under debian 10
AddOption('--with-thread-sanitizer', action='store_true', dest='with_thread_sanitizer', help='build with thread sanitizer enabled' , default=False)

cxx_flags = ['-std=c++17']
link_flags = ['-rdynamic']

if GetOption('with_thread_sanitizer'):
	cxx_flags.append('-fsanitize=thread')
	link_flags.append('-fsanitize=thread')

cpp17 = Environment(
	CCFLAGS=['-Wall', '-ggdb3', '-O0'],
	CXXFLAGS=cxx_flags,
	LIBS=['boost_stacktrace_backtrace', 'dl'],
	LINKFLAGS=link_flags,
	CPPDEFINES=['BOOST_STACKTRACE_USE_BACKTRACE'],
)

cpp17.ParseConfig('pkg-config --cflags --libs  glib-2.0 gstreamer-1.0 gstreamer-sdp-1.0 gstreamer-webrtc-1.0 json-glib-1.0 libsoup-2.4')

cpp17.Program('sendrecv', ['webrtc-sendrecv.c'])
cpp17.Program('send', ['webrtc-send.cpp'])
cpp17.Program('webrtc-unidirectional-h264', ['webrtc-unidirectional-h264.c'])
cpp17.Program(['rtsp_to_webrtc.cpp'])
cpp17.Program('h264streamer', ['h264streamer.cpp']) # compilable under debian 10
cpp17.Program('h264streamer_thread', ['h264streamer_thread.cpp'])

